if (isLoginModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'com.jakewharton.butterknife'
android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        if (isLoginModule.toBoolean()) {
            applicationId "cn.cqs.login"
        }
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }
    sourceSets {
        main {
            if (isLoginModule.toBoolean()) {
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    //避免资源文件的冲突 模块中加入以模块命名的前缀
    //resourcePrefix "login_"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    def deps = rootProject.ext.dependencies
    implementation deps.appcompat
    implementation project(':common')
    annotationProcessor deps.glideCompiler
    annotationProcessor deps.arouterCompiler
    annotationProcessor deps.butterknifeCompiler
}
